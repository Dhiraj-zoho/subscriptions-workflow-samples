
/*
	
  This is a sample function written for automating your workflow in Zoho Subscriptions, a recurring billing and subscription
  management software.

  Module           - Subscriptions.
  Predefined event - New Subscription.

  Place your {{connection name}} in "YOUR_CONNECTION_NAME" variable.

  * This custom function is used to convert a Lead in Zoho CRM to a Contact/Account.
	
*/

customer = subscriptions.get("customer");
customerEmail = customer.get("email");

// -- Search the lead with the email-address in Zoho CRM.
searchParams = "criteria=" + "(Email:equals:" + customerEmail + ")";
url = "https://www.zohoapis.com/crm/v2/Leads/search?" + searchParams;
response = invokeurl
[
	url :url
	type :GET
	connection:"YOUR_CONNECTION_NAME"
];

// Retrieving the details of the search result.
if(response == null || response.isEmpty())
{
	sendmail
	[
		from :zoho.adminuserid
		to :zoho.adminuserid
		subject :"Error occured in converting CRM Lead Custom Function"
		message :"No Lead found for the customer's Email address(" + customerEmail + ") in Zoho CRM. Please check manually from your end."
	]
}
else if(response.contains("status") && response.get("status").equals("error"))
{
	sendmail
	[
		from :zoho.adminuserid
		to :zoho.adminuserid
		subject :"Error occured in converting CRM Lead Custom Function"
		message :"<br><b>Error Message</b><br>" + response.get("message")
	]
}
else if(response.contains("data"))
{
	leads = response.get("data");
	if(leads.size() == 1)
	{
		leadID = leads.get(0).get("id");
		values_map = Map();
		values_map.put("overwrite",true);
		if(!customer.get("zcrm_account_id").isBlank())
		{
			values_map.put("Accounts",customer.get("zcrm_account_id"));
		}
		else if(!customer.get("zcrm_contact_id").isBlank())
		{
			values_map.put("Contacts",customer.get("zcrm_contact_id"));
		}
		response = zoho.crm.convertLead(leadID.toNumber(),values_map);
		if(response.contains("status") && response.get("status").equals("error"))
		{
			sendmail
			[
				from :zoho.adminuserid
				to :zoho.adminuserid
				subject :"Error occured in converting CRM Lead Custom Function"
				message :"Error occured while converting the lead in Zoho CRM. Please convert manually."
			]
		}
	}
	else
	{
		sendmail
		[
			from :zoho.adminuserid
			to :zoho.adminuserid
			subject :"Error occured in converting CRM Lead Custom Function"
			message :"More than one Lead found with the same Email Address(" + customerEmail + "). Please convert the lead manually."
		]
	}
}
